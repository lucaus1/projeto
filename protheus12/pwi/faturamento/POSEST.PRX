/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR260  ³ Autor ³ Eveli Morasco         ³ Data ³ 01/03/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Posicao dos Estoques                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo Pim.³09/12/97³09827A³Ajuste da descricao p/ 30 caracteres.     ³±±
±±³Fernando J. ³25/09/98³17720A³ Corre‡„o no Salto de Linhas.             ³±±
±±³Fernando J. ³02/12/98³18752A³A fun‡„o PesqPictQT foi substituida pela  ³±±
±±³            ³        ³      ³PesqPict.                                 ³±±
±±³Fernando J. ³21/12/98³18920A³Possibilitar filtragem pelo usuario.      ³±±
±±³Rodrigo Sart³08/02/99³META  ³Avaliacao da qtd empenhada prevista.      ³±±
±±³Cesar       ³30/03/99³XXXXXX³Manutencao na SetPrint()                  ³±±
±±³Patricia Sal³28/01/00³002121³Aumento da picture dos campos.            ³±±
±±³Jeremias    ³09.02.00³Melhor³Validacao da comparacao dos valores e da  ³±±
±±³            ³        ³      ³qtde quando do calculo do custo medio.    ³±±
±±³Macedo      ³07/06/02³      ³Conversao para RDMAKE                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

User Function POSEST()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

 Titulo   := "Relacao da Posicao do Estoque"
 cTITULO  := TITULO
 wnrel    := "MATR260"
 cDesc1   := "Este relatorio emite a posicao dos saldos e empenhos de cada  produto"
 cDesc2   := "em estoque. Ele tambem mostrara' o saldo disponivel ,ou seja ,o saldo"
 cDesc3   := "subtraido dos empenhos."
 cString  := "SB1"
 aOrd     := {OemToAnsi(" Por Codigo         "),OemToAnsi(" Por Tipo           "),OemToAnsi(" Por Descricao     "),OemToAnsi(" Por Grupo        "),OemToAnsi(" Por Almoxarifado   ")}
                                                                                                                                           
 lGo      := .F.
 lEnd     := .F.
 Tamanho  := "M"
 cTAM     := TAMANHO
 nTIPO    := 15
 _ALMATU  :=SPACE(2)
 _DESCALM:=SPACE(14)
 
   
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
 aReturn  := {OemToAnsi("Zebrado"), 1,OemToAnsi("Administracao"), 2, 2, 1, "",1 }   //###
 nLastKey := 0
 cPerg := "MTR260"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                                ³
//³ mv_par01     // Aglutina por: Almoxarifado / Filial / Empresa       ³
//³ mv_par02     // Filial de       *                                   ³
//³ mv_par03     // Filial ate      *                                   ³
//³ mv_par04     // almoxarifado de   *                                 ³
//³ mv_par05     // almoxarifado ate     *                              ³
//³ mv_par06     // codigo de       *                                   ³
//³ mv_par07      // codigo ate      *                                  ³
//³ mv_par08     // tipo de         *                                   ³
//³ mv_par09     // tipo ate        *                                   ³
//³ mv_par10     // grupo de        *                                   ³
//³ mv_par11     // grupo ate       *                                   ³
//³ mv_par12     // descricao de    *                                   ³
//³ mv_par13     // descricao ate   *                                   ³
//³ mv_par14     // imprime produtos: Todos /Positivos /Negativos       ³
//³ mv_par15     // Saldo a considerar : Atual / Fechamento / Movimento ³
//³ mv_par16     // Qual Moeda (1 a 5)                                  ³
//³ mv_par17     // Aglutina por UM ?(S)im (N)ao                        ³
//³ mv_par18     // Lista itens zerados ? (S)im (N)ao                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

pergunte(cPerg,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

wnrel:=SetPrint(cString,wnrel,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,aOrd,,Tamanho)
If nLastKey = 27
	Set Filter to
	Return
Endif

SetDefault(aReturn,cString)
If nLastKey = 27
	Set Filter to
	Return
Endif

mv_par16 := If( ((mv_par16 < 1) .Or. (mv_par16 > 5)),1,mv_par16 )
Tipo     := IIF(aReturn[4]==1,15,18)

If Type("NewHead")#"U"
	Titulo := (NewHead+" ("+AllTrim( aOrd[ aReturn[ 8 ] ] )+")")
Else
	Titulo += " ("+AllTrim( aOrd[ aReturn[ 8 ] ] )+")"
EndIf

cFileTRB := ""
RptStatus( { | lEnd | cFileTRB := r260Select( @lEnd ) },Titulo+": Preparacao..." )

If !Empty( cFileTRB )
	RptStatus({|lEnd| R260Imprime( @lEnd,cFileTRB,Titulo,wNRel,Tamanho,Tipo,aReturn[ 8 ] )},titulo)
EndIf

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R260SELECT³ Autor ³ Ben-Hur M. Castilho   ³ Data ³ 20/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Preparacao do Arquivo de Trabalho p/ Relatorio             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR260                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function R260Select()

cFileTRB := ""
cIndxSB1 := ""
cIndxKEY := ""
aSizeQT  := TamSX3( "B2_QATU" )
aSizeVL  := TamSX3( "B2_VATU1")
aSaldo   := {}
nQuant   := 0
nValor   := 0
nQuantR  := 0
nValorR  := 0

aCampos := { { "FILIAL","C",02,00 },;
{ "CODIGO","C",15,00 },;
{ "Local ","C",02,00 },;
{ "TIPO  ","C",02,00 },;
{ "GRUPO ","C",04,00 },;
{ "DESCRI","C",21,00 },;
{ "UM    ","C",02,00 },;
{ "VALORR","N",aSizeVL[ 1 ]+1,aSizeVL[ 2 ] },;
{ "QUANTR","N",aSizeQT[ 1 ]+1,aSizeQT[ 2 ] },;
{ "VALOR ","N",aSizeVL[ 1 ]+1,aSizeVL[ 2 ] },;
{ "QUANT ","N",aSizeQT[ 1 ]+1,aSizeQT[ 2 ] } }

If (mv_par01 == 1)
	
	If (aReturn[ 8 ] == 5)
		cIndxKEY := "Local+"
	Else
		cIndxKEY := "FILIAL+"
	EndIf
	
	If (mv_par17 == 1)
		cIndxKEY += "UM+"
	EndIf
	
	Do Case
		Case (aReturn[ 8 ] == 1)
			cIndxKEY += "CODIGO+Local"
		Case (aReturn[ 8 ] == 2)
			cIndxKEY += "TIPO+CODIGO+Local"
		Case (aReturn[ 8 ] == 3)
			cIndxKEY += "DESCRI+CODIGO+Local"
		Case (aReturn[ 8 ] == 4)
			cIndxKEY += "GRUPO+CODIGO+Local"
		Case (aReturn[ 8 ] == 5)
			cIndxKEY += "CODIGO+FILIAL"
	EndCase
Else
	
	If (aReturn[ 8 ] == 5)
		cIndxKEY := "Local+"
	Else
		cIndxKEY := ""
	EndIf
	
	If (mv_par17 == 1)
		cIndxKEY += "UM+"
	EndIf
	
	Do Case
		Case (aReturn[ 8 ] == 1)
			cIndxKEY += "CODIGO+FILIAL+Local"
		Case (aReturn[ 8 ] == 2)
			cIndxKEY += "TIPO+CODIGO+FILIAL+Local"
		Case (aReturn[ 8 ] == 3)
			cIndxKEY += "DESCRI+CODIGO+FILIAL+Local"
		Case (aReturn[ 8 ] == 4)
			cIndxKEY += "GRUPO+CODIGO+FILIAL+Local"
		Case (aReturn[ 8 ] == 5)
			cIndxKEY += "CODIGO+FILIAL"
	EndCase
EndIf

cIndxSB1 := CriaTrab( nil,.F. )
While ((cFileTRB := CriaTrab( nil,.F. )) == cIndxSB1) ; EndDo

DbSelectArea( "SB1" )
IndRegua( "SB1",cIndxSB1,"B1_COD+B1_FILIAL",,,OemToAnsi("Selecionando Registros..."))   //

DbSelectArea( 0 )
DbCreate( cFileTRB,aCampos )

DbUseArea( .F.,,cFileTRB,cFileTRB,.F.,.F. )
IndRegua( cFileTRB,cFileTRB,cIndxKEY,,,OemToAnsi("Organizando Arquivo..."))   //

DbSelectArea( "SB2" )
DbGoTop()

SetRegua( LastRec() )

While !Eof()
	
	IncRegua()
	
	If (((SB2->B2_FILIAL >= MV_PAR02) .And. (SB2->B2_FILIAL <= MV_PAR03)) .And. ;
		((SB2->B2_Local  >= MV_PAR04) .And. (SB2->B2_Local  <= MV_PAR05)) .And. ;
		((SB2->B2_COD    >= MV_PAR06) .And. (SB2->B2_COD    <= MV_PAR07)))
		
		DbSelectArea( "SB1" )
		
		If ((DbSeek( SB2->B2_COD+SB2->B2_FILIAL,.F. ) .Or.  ;
			DbSeek( SB2->B2_COD,.F. ))               .And. ;
			(SB2->B2_COD == SB1->B1_COD))
			
			If (((SB1->B1_TIPO  >= MV_PAR08) .And. (SB1->B1_TIPO  <= MV_PAR09)) .And. ;
				((SB1->B1_GRUPO >= MV_PAR10) .And. (SB1->B1_GRUPO <= MV_PAR11)) .And. ;
				((SB1->B1_DESC  >= MV_PAR12) .And. (SB1->B1_DESC  <= MV_PAR13)) .And. ;
				((!Empty(aReturn[7]) .And. &(aReturn[7])).Or.Empty(aReturn[7])))
				
				Do Case
					Case (mv_par15 == 1)
						nQuant := SB2->B2_QATU
					Case (mv_par15 == 2)
						nQuant := SB2->B2_QFIM
					OtherWise
						nQuant := (aSaldo := CalcEst( SB2->B2_COD,SB2->B2_Local,dDataBase+1,SB2->B2_FILIAL ))[ 1 ]
				EndCase
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Verifica se devera ser impresso itens zerados                ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				DbSelectArea( "SB2" )
				If (mv_par18 == 2 ) .And. (nQuant = 0)
					dbSkip()
					Loop
				EndIf
				
				DbSelectArea( "SB1" )
				If ((mv_par14 == 1)                      .Or. ;
					((mv_par14 == 2) .And.(nQuant >= 0))  .Or. ;
					((mv_par14 == 3) .And.(nQuant < 0)))
					
					Do Case
						Case (mv_par15 == 1)
							nValor := SB2->(FieldGet( FieldPos( "B2_VATU"+Str( mv_par16,1 ) ) ))
						Case (mv_par15 == 2)
							nValor := SB2->(FieldGet( FieldPos( "B2_VFIM"+Str( mv_par16,1 ) ) ))
						OtherWise
							nValor := aSaldo[ 1+mv_par16 ]
					EndCase
					
					If QtdComp(nValor) < QtdComp(0)
						nValor := (SB2->(FieldGet( FieldPos( "B2_CM"+Str( mv_par16,1 ) ) ))*nQuant)
					EndIf
					
					nQuantR := SB2->B2_QEMP+AvalQtdPre("SB2",1)+SB2->B2_RESERVA+SB2->B2_QEMPSA
					nValorR := ((QtdComp(nValor)/QtdComp(nQuant))*QtdComp(nQuantR))
					
					DbSelectArea( cFileTRB )
					DbAppend()
					
					FIELD->FILIAL := SB2->B2_FILIAL
					FIELD->CODIGO := SB2->B2_COD
					FIELD->Local  := SB2->B2_Local
					FIELD->TIPO   := SB1->B1_TIPO
					FIELD->GRUPO  := SB1->B1_GRUPO
					FIELD->DESCRI := SB1->B1_DESC
					FIELD->UM     := SB1->B1_UM
					FIELD->QUANTR := nQuantR
					FIELD->VALORR := nValorR
					FIELD->QUANT  := nQuant
					FIELD->VALOR  := nValor
					
					DbCommit() ; DbUnLock()
				EndIf
			EndIf
		EndIf
		
		DbSelectArea( "SB2" )
	EndIf
	
	DbSkip()
EndDo

DbSelectArea( "SB1" )
RetIndex( "SB1" )
Set Filter to
FErase( cIndxSB1+OrdBagExt() )

Return( cFileTRB )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³R260IMPRIM³ Autor ³ Ben-Hur M. Castilho   ³ Data ³ 20/11/96 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Preparacao do Arquivo de Trabalho p/ Relatorio             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR260                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function R260Imprime()

#define DET_SIZE  13

#define DET_CODE   1
#define DET_TIPO   2
#define DET_GRUP   3
#define DET_DESC   4
#define DET_UM     5
#define DET_FL     6
#define DET_ALMX   7
#define DET_SALD   8
#define DET_EMPN   9
#define DET_DISP  10
#define DET_VEST  11
#define DET_VEMP  12
#define DET_KEYV  13

#define ACM_SIZE   6

#define ACM_CODE   1
#define ACM_SALD   2
#define ACM_EMPN   3
#define ACM_DISP   4
#define ACM_VEST   5
#define ACM_VEMP   6

aPrnDET   := nil
aTotUM    := nil
aTotORD   := nil

cLPrnCd   := ""
nInKeyV   := 0

lPrintCAB := .F.
lPrintDET := .F.
lPrintTOT := .F.
lPrintOUT := .F.
lPrintLIN := .F.

nTotValEst:=0
nTotValEmp:=0
nTotValSal:=0
nTotValRPR:=0
nTotValRes:=0

cPicture  := PesqPict("SB2", If( (mv_par15 == 1),"B2_QATU","B2_QFIM" ),14 )
cPicVal    := PesqPict("SB2","B2_VATU"+Str(mv_par16,1),15)

Li    := 80
M_Pag := 1

cCab01 := OemToAnsi("CODIGO          TP GRUP DESCRICAO             UM FL ALM   SALDO       DESCRICAO DO     ESTOQUE      ___________V A L O R___________")        //
cCab02 := OemToAnsi("                                                          EM ESTOQUE  ALMOXARIFADO     DISPONIVEL    EM ESTOQUE          CUSTO MED ")        //
//  	                                   123456789012345 12 1234 123456789012345678901 12 12 12 999,999,999.99 999,999,999.99 9999,999,999.99 9999,999,999.99 9999,999,999.99
//      	                               0         1         2         3         4         5         6         7         8         9        10        11        12        13
//          	                           0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012

DbSelectArea( cFileTRB ) ; DbGoTop()

While !Eof()
	
	If (LastKey() == 286) ; Exit ; EndIf
	
	If (aPrnDET == nil)
		
		aPrnDET := Array( DET_SIZE )
		
		aPrnDET[ DET_CODE ] := FIELD->CODIGO
		aPrnDET[ DET_TIPO ] := FIELD->TIPO
		aPrnDET[ DET_GRUP ] := FIELD->GRUPO
		aPrnDET[ DET_DESC ] := FIELD->DESCRI
		aPrnDET[ DET_UM   ] := FIELD->UM
		
		aPrnDET[ DET_FL   ] := ""
		aPrnDET[ DET_ALMX ] := ""
		aPrnDET[ DET_SALD ] := 0
		aPrnDET[ DET_EMPN ] := 0
		aPrnDET[ DET_DISP ] := 0
		aPrnDET[ DET_VEST ] := 0
		aPrnDET[ DET_VEMP ] := 0
		
		aPrnDET[ DET_KEYV ] := ""
	EndIf
	
	If (mv_par17 == 1) .And. (aTotUM == nil)
		
		aTotUM := { FIELD->UM,0,0,0,0,0 }
	EndIf
	
	If (((aReturn[ 8 ] == 2) .Or. (aReturn[ 8 ] == 4)) .And. (aTotORD == nil))
		
		aTotORD := { If( (aReturn[ 8 ] == 2),FIELD->TIPO,FIELD->GRUPO ),0,0,0,0,0 }
	EndIf
	
	Do Case
		Case (mv_par01 == 1)
			
			aPrnDET[ DET_FL   ] := FIELD->FILIAL
			aPrnDET[ DET_ALMX ] := FIELD->Local
			
		Case ((mv_par01 == 2) .And. (aPrnDET[ DET_KEYV ] == ""))
			
			aPrnDET[ DET_FL   ] := FIELD->FILIAL
			aPrnDET[ DET_ALMX ] := If( (aReturn[ 8 ] == 5),FIELD->Local,"**" )
			
		Case ((mv_par01 == 3) .And. (aPrnDET[ DET_KEYV ] == ""))
			
			aPrnDET[ DET_FL   ] := "**"
			aPrnDET[ DET_ALMX ] := If( (aReturn[ 8 ] == 5),FIELD->Local,"**" )
	EndCase
	
	If aPrnDET[ DET_KEYV ] == ""
		Do Case
			Case (mv_par01 == 1)
				If (aReturn[ 8 ] == 5)
					aPrnDET[ DET_KEYV ] := FIELD->Local+FIELD->CODIGO+FIELD->FILIAL
				Else
					aPrnDET[ DET_KEYV ] := FIELD->CODIGO+FIELD->FILIAL+FIELD->Local
				Endif
			Case (mv_par01 == 2)
				If (aReturn[ 8 ] == 5)
					aPrnDET[ DET_KEYV ] := FIELD->Local+FIELD->CODIGO+FIELD->FILIAL
				Else
					aPrnDET[ DET_KEYV ] := FIELD->CODIGO+FIELD->FILIAL
				Endif
			Case (mv_par01 == 3)
				If (aReturn[ 8 ] == 5)
					aPrnDET[ DET_KEYV ] := FIELD->Local+FIELD->CODIGO
				Else
					aPrnDET[ DET_KEYV ] := FIELD->CODIGO
				Endif
		EndCase
	EndIf
	
	aPrnDET[ DET_SALD ] += FIELD->QUANT
	aPrnDET[ DET_EMPN ] += FIELD->QUANTR
	aPrnDET[ DET_DISP ] += (FIELD->QUANT-FIELD->QUANTR)
	aPrnDET[ DET_VEST ] += FIELD->VALOR
	aPrnDET[ DET_VEMP ] += FIELD->VALORR
	
	If (mv_par17 == 1)
		
		aTotUM[ ACM_SALD ] += FIELD->QUANT
		aTotUM[ ACM_EMPN ] += FIELD->QUANTR
		aTotUM[ ACM_DISP ] += (FIELD->QUANT-FIELD->QUANTR)
		aTotUM[ ACM_VEST ] += FIELD->VALOR
		aTotUM[ ACM_VEMP ] += FIELD->VALORR
	EndIf
	
	If ((aReturn[ 8 ] == 2) .Or. (aReturn[ 8 ] == 4))
		
		aTotORD[ ACM_SALD ] += FIELD->QUANT
		aTotORD[ ACM_EMPN ] += FIELD->QUANTR
		aTotORD[ ACM_DISP ] += (FIELD->QUANT-FIELD->QUANTR)
		aTotORD[ ACM_VEST ] += FIELD->VALOR
		aTotORD[ ACM_VEMP ] += FIELD->VALORR
	EndIf
	
	DbSkip()
	
	Do Case
		Case (mv_par01 == 1)
			If (aReturn[ 8 ] == 5)
				lPrintDET := !(aPrnDET[ DET_KEYV ] == FIELD->Local+FIELD->CODIGO+FIELD->FILIAL)
			Else
				lPrintDET := !(aPrnDET[ DET_KEYV ] == FIELD->CODIGO+FIELD->FILIAL+FIELD->Local)
			Endif
		Case (mv_par01 == 2)
			If (aReturn[ 8 ] == 5)
				lPrintDET := !(aPrnDET[ DET_KEYV ] == FIELD->Local+FIELD->CODIGO+FIELD->FILIAL)
			Else
				lPrintDET := !(aPrnDET[ DET_KEYV ] == FIELD->CODIGO+FIELD->FILIAL)
			Endif
		Case (mv_par01 == 3)
			If (aReturn[ 8 ] == 5)
				lPrintDET := !(aPrnDET[ DET_KEYV ] == FIELD->Local+FIELD->CODIGO)
			Else
				lPrintDET := !(aPrnDET[ DET_KEYV ] == FIELD->CODIGO)
			Endif
	EndCase
	
	Do Case
		Case ((mv_par17 == 1) .And. ;
			!(aTotUM[ ACM_CODE ] == FIELD->UM))
			
			lPrintTOT := .T.
		Case (( (aReturn[ 8 ] == 2) .Or. (aReturn[ 8 ] == 4) ) .And. ;
			!(aTotORD[ ACM_CODE ] == If( (aReturn[ 8 ] == 2),FIELD->TIPO,FIELD->GRUPO )))
			
			lPrintTOT := .T.
	EndCase
	
	If lPrintDET .Or. lPrintTOT
		
		If (Li > 58)
			
			Cabec( cTitulo,cCab01,cCab02,wNRel,cTam,nTipo )
		EndIf
		
		Do Case
			Case !(aPrnDET[ DET_CODE ] == cLPrnCd)
				
				cLPrnCd := aPrnDET[ DET_CODE ] ; lPrintCAB := .T.
		EndCase
		
		If lPrintCAB .Or. lPrintOUT
			
			@ Li,000 PSay aPrnDET[ DET_CODE ]
			@ Li,016 PSay aPrnDET[ DET_TIPO ]
			@ Li,019 PSay aPrnDET[ DET_GRUP ]
			@ Li,024 PSay aPrnDET[ DET_DESC ]
			@ Li,046 PSay aPrnDET[ DET_UM   ]
			
			lPrintCAB := .F. ; lPrintOUT := .F.
		EndIf
		
		@ Li,049 PSay aPrnDET[ DET_FL   ]
		@ Li,052 PSay aPrnDET[ DET_ALMX ]

        
        IF aPrnDET[ DET_ALMX ]<>_ALMATU .AND. aPrnDET[ DET_ALMX ]<>"**"
		   _ALMATU:=aPrnDET[ DET_ALMX ]
           _cArea := Alias()
           DBSELECTAREA("SZZ")
           DBSETORDER(1)
           DBSEEK(xFilial("SZZ")+_ALMATU)
		   IF FOUND()
		      _DESCALM:=" "+LEFT(SZZ->ZZ_DESC,13)
		     ELSE
		      _DESCALM:=SPACE(14)
		   ENDIF
		   DBSELECTAREA(_cArea)
		ENDIF   
		
		@ Li,055 PSay aPrnDET[ DET_SALD ] Picture cPicture
//		@ Li,070 PSay aPrnDET[ DET_EMPN ] Picture cPicture
        @ LI,070 PSay _DESCALM
		@ Li,085 PSay aPrnDET[ DET_DISP ] Picture cPicture
		@ Li,101 PSay aPrnDET[ DET_VEST ] Picture cPicVal
//		@ Li,117 PSay aPrnDET[ DET_VEMP ] Picture cPicVal
        IF aPrnDET[ DET_SALD ] <> 0
           @ Li,117 PSAY (aPrnDET[ DET_VEST ]/aPrnDET[ DET_SALD ]) Picture cPicVal
		ENDIF
		
		nTotValSal+=aPrnDET[ DET_SALD ]
//		nTotValRpr+=aPrnDET[ DET_EMPN ]
		nTotValRes+=aPrnDET[ DET_DISP ]
		nTotValEst+=aPrnDET[ DET_VEST ]
//		nTotValEmp+=aPrnDET[ DET_VEMP ]
		
                aPrnDET := nil ; Li:=LI+1
		
		If (((aReturn[ 8 ] == 2) .Or. (aReturn[ 8 ] == 4)) .And. ;
			!(aTotORD[ ACM_CODE ] == If( (aReturn[ 8 ] == 2),FIELD->TIPO,FIELD->GRUPO )))
			
                        Li:=LI+1
			
                        @ Li,016 PSay OemToAnsi("Total do ")+If( (aReturn[ 8 ] == 2),OemToAnsi("Tipo"),OemToAnsi("Grupo"))+" : "+aTotORD[ ACM_CODE ]   //######
			
			@ Li,055 PSay aTotORD[ ACM_SALD ] Picture cPicture
			@ Li,070 PSay aTotORD[ ACM_EMPN ] Picture cPicture
			@ Li,085 PSay aTotORD[ ACM_DISP ] Picture cPicture
			@ Li,101 PSay aTotORD[ ACM_VEST ] Picture cPicVal
			@ Li,117 PSay aTotORD[ ACM_VEMP ] Picture cPicVal
			
                        Li:=LI+1
			
			aTotORD   := nil ; lPrintLIN := .T.
			lPrintTOT := .F. ; lPrintOUT := .T.
		EndIf
		
		If ((mv_par17 == 1) .And. ;
			!(aTotUM[ ACM_CODE ] == FIELD->UM))
			
                        Li:=LI+1
			
                        @ Li,016 PSay OemToAnsi("Total Unidade Medida : ")+aTotUM[ ACM_CODE ]   //
			@ Li,055 PSay aTotUM[ ACM_SALD ] Picture cPicture
			@ Li,070 PSay aTotUM[ ACM_EMPN ] Picture cPicture
			@ Li,085 PSay aTotUM[ ACM_DISP ] Picture cPicture
			@ Li,101 PSay aTotUM[ ACM_VEST ] Picture cPicVal
			@ Li,117 PSay aTotUM[ ACM_VEMP ] Picture cPicVal
			
                        Li:=LI+1
			
			aTotUM    := nil ; lPrintLIN := .T.
			lPrintTOT := .F. ; lPrintOUT := .T.
		EndIf
		
		If lPrintLIN
                        Li:=LI+1 ; lPrintLIN := .F.
		EndIf
	EndIf
EndDo

If nTotValSal + nTotValRPR + nTotValRes + nTotValEst + nTotValEmp > 0
	If Li > 58
		Cabec(cTitulo,cCab01,cCab02,wnRel,cTam,nTipo)
	EndIf
	Li += If(mv_par17#1,1,0)
        @ Li,016 PSay OemToAnsi("Total Geral : ") // 
	@ Li,055 PSay nTotValSal Picture cPicture
	@ Li,070 PSay nTotValRPR Picture cPicture
	@ Li,085 PSay nTotValRes Picture cPicture
	@ Li,101 PSay nTotValEst Picture cPicVal
	@ Li,117 PSay nTotValEmp Picture cPicVal
EndIf

If (LastKey() == 286)
        @ pRow()+1,00 PSay OemToAnsi("CANCELADO PELO OPERADOR.")     //
Else
        Roda( LastRec(), OemToAnsi("Registro(s) processado(s)"),cTam )    //
EndIf

SET DEVICE TO SCREEN

MS_FLUSH()

If (aReturn[ 5 ] == 1)
	SET PRINTER TO
	OurSpool( wNRel )
Endif

DbSelectArea( cFileTRB )  ; DbCloseArea()
FErase( cFileTRB+".DBF" ) ; FErase( cFileTRB+OrdBagExt() )

DbSelectArea( "SB1" )

Return( nil )
